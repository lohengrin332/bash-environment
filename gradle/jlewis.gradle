// Adapted from a similar solution for Windows by https://twitter.com/mrhaki, found here: https://mrhaki.blogspot.com/2009/11/gradle-goodness-get-notified-about.html
class NotifySendListener extends BuildAdapter {
    void buildFinished(BuildResult result) {
        String tag = "<b>${result.getGradle().getStartParameter().getCurrentDir().getName()}</b>:\n"
        if (result.failure) {
            notifySend(
                "Gradle build failure",
                tag + result.failure.message,
                '/usr/share/icons/elementary-xfce/actions/48/gtk-delete.png'
            )
        } else {
            notifySend(
                "Gradle build finished",
                tag + result.getGradle().getStartParameter().getTaskNames().join('\n')
            )
        }
    }

    void notifySend(title, message = 'No message', icon = '/usr/share/icons/elementary-xfce/actions/48/gnome-run.png', sticky = true) {
        def cmd = [
            '/usr/bin/notify-send',
            title,
            message,
            '-c',
            'gradle',
            '-i',
            icon
        ]
        if (sticky) {
            cmd << '-t' << '0'
        }
        cmd.execute()
    }
}

def listener = new NotifySendListener()
gradle.addBuildListener listener
